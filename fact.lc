# definitions 
inc := \n.\f.\x.(f (n f x))

add := \n.\m.(n inc m)
mult := \n.\m.\f.(n (m f))

0  := \f.\x.x
1  := (inc 0)
2  := (inc 1)
3  := (inc 2)
4  := (inc 3)
5  := (inc 4)
6  := (inc 5)
7  := (inc 6)
8  := (inc 7)
9  := (inc 8)
10 := (inc 9)
11 := (inc 10)
12 := (inc 11)

true := \a.\b.a
false := \a.\b.b

and := \a.\b.(a b a)
or := \a.\b.(a a b)
not := \a.(a false true)

cons := \a.\b.\f.(f a b)
car := \p.(p true)
cdr := \p.(p false)

shiftinc := \p.(cons (cdr p) (inc (cdr p)))

dec := \n.(car (n shiftinc (cons 0 0)))
sub := \n.\m.(n dec m)
is0 := \n.(n (\x.false) true)

eq := \n.\m.(and (is0 (sub n m)) (is0 (sub m n)))

Y := \f.(\x.(f (x x)) \x.(f (x x)))

fact := (Y \f.\n.(is0 n 1 (mult n (f (dec n)))))

# run your expression at the end of the file
(fact 4)
